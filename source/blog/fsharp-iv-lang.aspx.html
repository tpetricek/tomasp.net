<!-- [info]

 - date: 2007-11-03T00:00:04.0000000
 - description: In the fourth article of the F# overview series, I will shortly describe how I understad the language oriented paradigm and how the F# language can be used for developing libraries using this paradigm.
 - tags: functional,asynchronous,meta-programming,f#
 - layout: article
 - title: F# Overview (IV.) - Language Oriented Programming
 - url: fsharp-iv-lang.aspx

[/info] -->
<!-- [abstract]
<p>In the fourth article of the F# overview series, I will shortly describe how I understad the language oriented paradigm and how the F# language can be used for developing libraries using this paradigm. We will look how discriminated unions relate to this paradigm and at three specific features that support this paradigm, namely <em>active patterns</em>, <em>computation expressions</em> and <em>quotations</em>.</p>

<p>Defining precisely what the term <em>language oriented programming</em> means in context of the F# language would be difficult,
  so I will instead explain a few examples that will demonstrate how I understand it. In general, the goal of language oriented
  programming is to develop a <em>language</em> that would be suitable for some (more specific) class of tasks and use this language 
  for solving these tasks. Of course, developing a real programming language is extremely complex problem, so there are several ways
  for making it easier. As the most elementary example, you can look at XML files (with certain schema) as language that 
  are processed by your program and solve some specific problem (for example configuring the application). As a side note,
  I should mention that I'm not particularly happy with the term ‘language’ in this context, because the term can be used
  for describing a wide range of techniques from very trivial constructs to a complex object-oriented class libraries, 
  but I have not seen any better term for the class of techniques that I’m going to talk about.</p>
[/abstract] -->

<h1>F# Overview (IV.) - Language Oriented Programming</h1>
<p>Defining precisely what the term <em>language oriented programming</em> means in context of the F# language would be difficult,
  so I will instead explain a few examples that will demonstrate how I understand it. In general, the goal of language oriented
  programming is to develop a <em>language</em> that would be suitable for some (more specific) class of tasks and use this language 
  for solving these tasks. Of course, developing a real programming language is extremely complex problem, so there are several ways
  for making it easier. As the most elementary example, you can look at XML files (with certain schema) as language that 
  are processed by your program and solve some specific problem (for example configuring the application). As a side note,
  I should mention that I'm not particularly happy with the term ‘language’ in this context, because the term can be used
  for describing a wide range of techniques from very trivial constructs to a complex object-oriented class libraries, 
  but I have not seen any better term for the class of techniques that I’m going to talk about.</p>
  
<p>What I will focus on in this article is using languages inside F# - this means that the custom language will be always a subset
  of the F# language, but we will look at ways for giving it a different meaning than the standard F# code would have. In some articles
  you can also see the term <em>domain specific language</em>, which is quite close to what we're discussing here. The domain specific
  language is a language suitable for solving some class of problems and in general it can be either <em>external</em>, meaning
  that it is a separate language (e.g. a XML file) or an <em>internal</em>, meaning that it is written using a subset of the host language.
  Using this terminology, we will focus on writing <em>internal DSLs</em> in F#.</p>  
<p>Since this term is not as widely used as functional or object oriented programming which we discussed in earlier articles in this series
  (<a href="/blog/fsharp-ii-functional.aspx">Functional Programming</a> [<a href="/blog/fsharp-ii-functional.aspx">^</a>], 
   <a href="/blog/fsharp-iii-oop.aspx">Object Oriented and Imperative Programming</a> [<a href="/blog/fsharp-iii-oop.aspx">^</a>]), let me 
   very quickly introduce why I believe that this is an important topic. I think the main reason why language oriented
   development is appealing paradigm is that it allows very smooth cooperation of people working on the project - 
   there are people who develop the language and those who use it. The language developers need to have advanced 
   knowledge of the technology (F#) and also of the problem that their language is trying to solve (e.g. some mathematical
   processing), but they don't need to know about all the problems that are being solved using the language. On the other
   side, the users of the language need only basic F# knowledge and they can fully focus on solving the real problems.</p>

<h2>Discriminated Union as Declarative Language</h2>
<p>Probably the simplest example of domain-specific language that can be embedded in the F# code is a discriminated union, 
  which can be used for writing declarative specifications of behavior or for example for representing and processing mathematical 
  expressions:</p>
<pre lang="fsharp">
&gt; type Expr = 
  | Binary of string * Expr * Expr
  | Var    of string 
  | Const  of int;;
(...)

&gt; let e = Binary("+", Const(2), Binary("*", Var("x"), Const(4)));;
val e : Expr
</pre>
<p>In this example we created a discriminated union and used it for building a value representing a mathematical expression.
  This is of course very primitive ‘language’, but when you implement functions for working with these values (for example
  differentiation or evaluation) you’ll get a simple language for processing mathematical expressions inside F#. Another problems
  that could be solved using this technique include for example configuration of some graphical user interface or 
  definition of template for some simple data manipulation.</p>

<h2>Active Patterns</h2>
<p>A language feature that is closely related to discriminated unions is called active patterns. Active patterns can be used for 
  providing different views on some data type, which allows us to hide the internal representation of the type and publish only 
  these views. Active patterns are similar to discriminated unions, because they can provide several views on a single value (in 
  the previous example we had a value that we could view either as <code>Binary</code>, <code>Var</code> or <code>Const</code>) and 
  similarly as constructors of discriminated union, active patterns can be used in pattern matching constructs.</p>
<p>A typical example, where a type can be viewed using different views is a complex number, which can be either viewed in a 
  Cartesian representation (real and imaginary part) or in a polar form (absolute value and phase). Once the module provides 
  these two views for a complex number type, the internal representation of the type can be hidden, because all users of the 
  type will work with the number using active patterns, which also makes it easy to change the implementation of the type as 
  needed.</p>
<p>It is recommended to use active patterns in public library API instead of exposing the names of discriminated union 
  constructors, because this makes it possible to change the internal representation without breaking the existing code. The 
  second possible use of active patterns is extending the ‘vocabulary’ of a language built using discriminated union. In the 
  following example we will implement an active pattern <code>Commutative</code> that allows us to decompose a value of type 
  <code>Expr</code> into a call to commutative binary operator:</p>
<pre lang="fsharp">
&gt; let (|Commutative|_|) x =
    match x with
    | Binary(s, e1, e2) when (s = "+") || (s = "*") -&gt; Some(s, e1, e2)
    | _ -&gt; None;;
val ( |Commutative|_| ) : Expr -&gt; (string * Expr * Expr) option
</pre>

<p>As you can see, the declaration of active pattern looks like a function declaration, but uses a strangely looking function 
  name. In this case we use the <code>(|PatternName|_|)</code> syntax, which declares a pattern that can return a successful match or 
  can fail. The pattern has a single argument (of type <code>Expr</code>) and returns an option type, which can be either <code>Some(...)</code>
  when the value matches the pattern or <code>None</code>. As we will show later, the patterns that can fail can be used in a match 
  construct, where you can test a value against several different patterns.</p>
<p>As demonstrated in this example, active patterns can be used in a similar sense in which you can use discriminated 
  unions to define a language for constructing the values. The key difference is that discriminated unions can be used 
  for building the value (meaning that they will be used by all users of the language) and active patterns are used for 
  decomposing the values and so they will be used in a code that interprets the language (written usually by the language 
  designer) or by some pre-processing or optimizing code (written by advanced users of the language).</p>

<p>In the next example we will look at one advanced example of using the numerical language that we define earlier. We 
  will implement a function that tests whether two expressions are equal using the commutativity rule, meaning that for example
  <code>10*(a+5)</code> will be considered as equal to <code>(5+a)*10</code>:</p>
<pre lang="fsharp">
&gt; let rec equal e1 e2 = 
    match e1, e2 with
    | Commutative(o1, l1, r1), Commutative(o2, l2, r2) -&gt;
       (o1 = o2) &amp;&amp; (equal l1 r2) &amp;&amp; (equal r1 l2)
    | _ -&gt; e1 = e2;;
val equal : Expr -&gt; Expr -&gt; bool

&gt; let e1 = Binary("*", Binary("+", Const(10), Var("x")), Const(4));;
  let e2 = Binary("*", Const(4), Binary("+", Var("x"), Const(10)));;
  equal e1 e2;;
val it : bool = true</pre>
  
<p>As you can see, implementing the <code>equal</code> function that uses the commutativity rule
  is much easier using the <code>Commutative</code> active pattern than it would be explicitly by testing
  if the value is a use of specific binary operator. Also, when we’ll introduce a new commutative operator,
  we’ll only need to modify the active pattern and the <code>equal</code> function will work correctly.</p>


<h2>Sequence comprehensions</h2>
<p>Before digging deeper into advanced language-oriented features of F#, I'll need to do a small 
  digression and talk about <em>sequence comprehensions</em>. This is a language construct that
  allows us to generate sequences, lists and arrays of data in F# and as we will see later it can
  be generalized to allow solving several related problems. Anyway, let's first look at an
  example that filters an F# list:</p>
<pre lang="fsharp">
&gt; let people = [ ("Joe", 55); ("John", 32); ("Jane", 24); ("Jimmy", 42) ];;
val people : (string * int) list

&gt; [ for (name, age) in people
    when age &lt; 30
    -&gt; name ];;
val it : string list = ["Jane"]</pre>
<p>In this example we first declared a list with some data and then used a sequence expression, wrapped
  between square brackets <code>[</code> and <code>]</code>, to select only some elements from the list. 
  The use of square brackets indicate that the result should be an F# list (you can also use <code>[| .. |]</code> 
  to get an array or <code>seq { .. }</code> to get a sequence as I'll show later). The code inside the
  comprehension can contain most of the ordinary F# expressions, but in this example I used one extension,
  the <code>when .. -&gt;</code> construct, which can be used for typical filtering and projection operations.
  The same code can be written like this:</p> 
<pre lang="fsharp">
&gt; [ for (name, age) in people do
    if (age &lt; 30) then
      yield name ];;
val it : string list = ["Jane"]</pre>
<p>In this example, we used an ordinary <code>for .. do</code> loop (in the previous example the <code>do</code>
  keyword was missing and we used <code>if .. then</code> condition instead of <code>when</code>. Finally, 
  returning a value from a sequence comprehension can be done using the <code>yield</code> construct.
  The point of this example is to demonstrate that the code inside the comprehension is not limited to 
  some specific set of expressions and can, in fact, contain very complex F# code. I will demonstrate the
  flexibility of sequence comprehensions in one more example - the code will generate all possible words
  (of specified length) that can be generated using the given alphabet:</p>
<pre lang="fsharp">
&gt; let rec generateWords letters start len =
    seq { for l in letters do
            let word = (start ^ l)
            if len = 1 then
              yield  word
            if len &gt; 1 then
              yield! generateWords letters word (len-1) }
val generateWords : #seq&lt;string&gt; -&gt; string -&gt; int -&gt; seq&lt;string&gt;
              
&gt; generateWords ["a"; "b"; "c"] "" 4;;
val it : seq&lt;string&gt; = seq ["aaaa"; "aaab"; "aaac"; "aaba"; ...]
</pre>
<p>This example introduces two interesting constructs. First of all, we're using <code>seq { .. }</code> expression
  to build the sequence, which is a lazy data structure, meaning that the code will be evaluated on demand. When 
  you ask for the next element, it will continue evaluating until it reaches <code>yield</code> construct,
  which returns a word and then it will block again (until you ask for the next element). The second interesting
  fact is that the code is recursive - the <code>generateWord</code> function calls itself using
  <code>yield!</code> construct, which first computes the elements from the given sequence and then
  continues with evaluation of the remaining elements in the current comprehension.</p>

<h2>F# Computation Expression</h2>
<p>The next F# feature that we will look at can be viewed as a generalization of the sequence comprehensions.
  In general, it allows you to declare blocks similar to the <code>seq { .. }</code> block that execute 
  the F# code in a slightly different way. In the case of <code>seq</code> this difference is that the 
  code can return multiple values using <code>yield</code>.</p>
<p>In the next example we will implement a similar block called <code>maybe</code> that performs some 
  computation and returns <code>Some(res)</code> when the computation succeeds, but it can also stop its 
  execution when some operation fails and return <code>None</code> immediately, without executing the
  rest of the code inside the block. Let's first implement a simple function that can either return some
  value or can fail and return <code>None</code>:</p>  
<pre lang="fsharp">
let readNum () =
  let s = Console.ReadLine()
  let succ,v = Int32.TryParse(s)
  if (succ) then Some(v) else None
</pre>

<p>Now, we can write a code that reads two numbers from the console and adds them together, producing
  a value <code>Some(a+b)</code>. However, when a call to <code>readNum</code> fails, we want to return
  <code>None</code> immediately without executing the second call to <code>readNum</code>. This is
  exactly what the <code>maybe</code> block will do (I'll show the implementation of the block shortly):</p>
<pre lang="fsharp">
let n = 
  maybe { do   printf "Enter a: "
          let! a = readNum()
          do   printf "Enter b: "
          let! b = readNum()
          return a + b }  
printf "Result is: %A" n</pre>
<p>The code inside the block first calls <code>printf</code> and then uses a <code>let!</code> construct
  to call the <code>readNum</code> function. This operation is called <em>monadic bind</em> and the implementation
  of <code>maybe</code> block specifies the behavior of this operation. Similarly, it can also specify
  behavior of the <code>do</code> and <code>return</code> operation, but in this example the <code>let!</code>
  is the most interesting, because it tests whether the computed value is <code>None</code> and stops the 
  execution in such case (otherwise it starts executing the rest of the block).
</p>

<p>Before looking at the implementation of the <code>maybe</code> block, let's look at the type of the
  functions that we'll need to implement. Every block (usually called <em>computation expression</em> in F#) is
  implemented by a <em>monadic builder</em> which has the following members that define elementary operators:</p>

<pre lang="fsharp">
// Signature of the builder for monad M
type MaybeBuilder with
  member Bind   : M&lt;'a&gt; * ('a -&gt; M&lt;'b&gt;) -&gt; M&lt;'b&gt;
  member Return : 'a -&gt; M&lt;'a&gt;
  member Delay  : (unit -&gt; M&lt;'a&gt;) -&gt; M&lt;'a&gt;
</pre>

<p>We'll shortly discuss how the F# compiler uses these members to execute the computation expression, but let me 
  first add a few short comments for those who are familiar with Haskell monads. The <code>Bind</code> and 
  <code>Return</code> members specify the standard monadic operators (known from Haskell), meaning that <code>Bind</code>
  is used when we use the <code>let!</code> operator in the code and <code>Return</code> is called when the
  computation expression contains <code>return</code> and finally, 
  the <code>Delay</code> member allows building monads that are executed lazily.</p>
  
<p>The computation expression block is just a syntactic extension that makes it possible to write a code that uses the 
  monadic operations, but is similar to an ordinary F# code. This means that the code inside the computation expression
  is simply translated to calls to the basic monadic operation, which we looked at earlier. The following example should
  put some light on the problem, because it shows how the F# compiler translates the code written using the <code>maybe</code>
  block:</p>
<pre lang="fsharp">
maybe.Delay(fun () -&gt;
  printf "Enter a"
  maybe.Bind(readNum(), fun a -&gt;
    printf "Enter b"
    maybe.Bind(readNum(), fun b -&gt;
      maybe.Return(a + b))</pre>         

<p>As we can see, the original code is split into single expressions and these are evaluated separately as
  arguments of the monadic operations. It is also important to note that the expression may not be evaluated,
  because this depends on the behavior of the monadic operation.</p>
  
<p>For example, let's analyze the third line,
  where a first call to the <code>Bind</code> operation occurs. The first argument will be evaluated asking 
  for a user input and will produce either <code>None</code> or <code>Some(n)</code>. The second argument
  is a function that takes one argument (<code>a</code>) and executes the rest of the computation expression.
  As you can see, the let binding in the original code was translated to a call to the <code>Bind</code> 
  operation which can perform some additional processing and change the semantics and then assign a value
  to the variable by calling the given function. Also note that the first argument of the <code>Bind</code>
  operation is a <em>monadic type</em> (in the signature presented above it was <code>M&lt;'a&gt;</code>, 
  while the argument of the function given as a second argument is ordinary type (unwrapped <code>'a</code>).
  This means that the monadic type can hold some additional information - in our <code>maybe</code> monad,
  the additional information is a possibility of the failure of the operation.</p>          

<p>Let's look at the implementation of the <code>maybe</code> monad now. The <code>Bind</code> operation will 
  test if the first argument is <code>Some(n)</code> and then it will call the function given as a second argument 
  with <code>n</code> as an argument. If the value of the first argument is <code>None</code> the <code>Bind</code> 
  operation just returns <code>None</code>. The second key operation is <code>Result</code> which simply wraps an 
  ordinary value into a monadic type - in our example it will take a value <code>a</code> (of type <code>'a</code>)
  and turn it into a value <code>Some(a)</code> (of type M&lt;'a&gt;):</p>

<pre lang="fsharp">
type M&lt;'a&gt; = option&lt;'a&gt;

let bind d f = 
  match d with
   | None -> None
   | Some(v) -> f v
let result v = Some(v)
let delay  f = f()
  
type MaybeBuilder() =
  member x.Bind(v, f) = bind v f
  member x.Return(v)  = result v
  member x.Delay(f)   = delay f 
  member x.Let(v, f)  = bind (result v) f
  
let maybe = MaybeBuilder()</pre>
<p>In this example we looked at computation expressions and implemented a simple <em>monadic builder</em> for 
  representing a computations that can fail. We implemented support only for basic language constructs (like
  <code>let</code> and <code>let!</code>), but in general the computation expression can allow using constructs
  like <code>if</code>, <code>try .. when</code> and other. For more information, please refer to [<a href="#fsintro4links">1</a>]. 
  Computation expressions are very powerful when you want to modify the behavior of the F# code, without changing the semantics
  of elementary expressions, for example by adding a possibility to fail (as we did in this example), or by executing the code 
  asynchronously (as <em>asynchronous workflows</em> [<a href="#fsintro4links">2</a>], which are part of the F# library do).</p>  
  
<h2>F# Meta-Programming and Reflection</h2>
<p>The last approach to language oriented programming that I’ll present in this overview is using <em>meta-programming</em>
  capabilities of the F# language and .NET runtime. In general the term ‘meta-programming’ means writing a program
  that treats code as data and manipulates with it in some way. In F# this technique can be used for translating a code
  written in F# to other languages or formats that can be executed in some other execution environment or it can be
  used for analysis of the F# code and for calculating some additional properties of this code.</p>

<p>The meta-programming capabilities of F# and .NET runtime can be viewed as a two separate and orthogonal parts. The .NET 
  runtime provides a way for discovering all the types and top-level method definitions in a running program: this API is 
  called <em>reflection</em>. F# <em>quotations</em> provide a second part of the full meta-programming support - they
  can be used for extracting an abstract syntax tree of members discovered using the .NET reflection mechanism
  (note that the F# <em>quotations</em> are a feature of the F# compiler and as such can’t be produced by 
  C# or VB compilers).</p>
  
<h3>.NET and F# Reflection</h3>
<p>The F# library also extends the .NET <code>System.Reflection</code> to give additional information about F# data types – 
  for example we can use the F# reflection library to examine possible values of the <code>Expr</code> type (discriminated 
  union) declared earlier:</p>
<pre lang="fsharp">
&gt; open Microsoft.FSharp.Reflection;;
&gt; let exprTy = typeof&lt;Expr&gt;
  if FSharpType.IsUnion(exprTy) then
    let opts = FSharpType.GetUnionCases(exprTy)
    opts |> Array.map (fun m -> m.Name)
  else
    [| |];;
val it : string[] = [| "Binary"; "Var"; "Const" |]</pre>
<p>An important part of the .NET reflection mechanism is the use of custom attributes, which can be used to annotate any 
  program construct accessible via reflection with additional metadata. The following example demonstrates the syntax for 
  attributes in F# by declaring <code>Documentation</code> attribute (simply by inheriting from the <code>System.Attribute</code> 
  base class) and also demonstrates how a static method in a class can be annotated with the attribute: </p>
<pre lang="fsharp">
type DocumentationAttribute(doc:string) =
  inherit System.Attribute()
  member x.Doc = doc

type Demo =
  [&lt;Documentation("Adds one to a given number")&gt;]
  static member AddOne x = x + 1</pre>
<p>Using the .NET <code>System.Reflection</code> library it is possible to examine members of the <code>Demo</code> type 
  including reading of the associated attributes (which are stored in the compiled DLL and are available at run-time):</p>
<pre lang="fsharp">
&gt; let ty = typeof&lt;Demo&gt;
  let mi = ty.GetMethod("AddOne")
  let at = mi.GetCustomAttributes
                (typeof&lt;DocumentationAttribute&gt;, false)
  (at.[0] :?&gt; DocumentationAttribute).Doc;;
val it : string = "Adds one to a given number"</pre>

<h3>F# Quotations</h3>
<p>F# quotations form the second part of the meta-programming mechanism, by allowing the capture of type-checked F# 
  expressions as structured terms. There are two ways for capturing quotations – the first way is to use quotation literals 
  and explicitly mark a piece of code as a quotation and the second way is to use <code>ReflectedDefinition</code> attribute, which instructs 
  the compiler to store quotation data for a specified top-level member. The following example demonstrates a few simple 
  quoted F# expressions – the quoted expressions are ordinary type-checked F# expressions wrapped between &lt;@@ and @@&gt;:</p>
<pre lang="fsharp">&gt; open Microsoft.FSharp.Quotations;;
&gt; &lt;@@ 1 + 1 @@&gt;
val it : Expr&lt;int&gt;

&gt; &lt;@@ (fun x -&gt; x + 1) @@&gt;
val it : Expr&lt;int -&gt; int&gt;</pre>
<p>Quotation processing is usually done on the raw representation of the quotations, which is represented by the non-generic 
  <code>Expr</code> type (however the type information about the quoted expression is still available dynamically via the <code>Type</code> property). 
  The following example implements a trivial evaluator for quotations. <code>GenericTopDefnApp</code> is an active pattern 
  that matches with the use of a function given as a first argument (in this example a plus operator), the <code>Int32</code> 
  pattern recognizes a constant of type <code>int</code>):</p>
<pre lang="fsharp">
&gt; open Microsoft.FSharp.Quotations.Patterns;;
&gt; open Microsoft.FSharp.Quotations.DerivedPatterns;;
&gt; let plusOp = &lt;@@ (+) @@&gt;
  let rec eval x =
    match x with
    | SpecificCall plusOp (_, [l; r]) -&gt;
        (eval l) + (eval r)
    | Int32(n) -&gt; 
        n
    | _ -&gt;        
        failwith "unknonw construct"  
val eval : Expr -&gt; int

&gt; let tst = &lt;@@ (1+2) + (3+4) @@&gt;
  eval tst
val it : int = 10</pre>


<h3>Quotation Templates and Splicing</h3>
<p>When generating quotations programmatically, it is often useful to build a quotation by combining several elementary
  quotations into a one, more complex quotation. This can be done by creating a <em>quotation template</em>, which is a quotation
  that contains one or more <em>holes</em>. Holes are written using the underscore symbol and define a place, where another quotation
  can be filled in the template. In the following example, we will look at a template that contains two holes and can be used
  for generating a quotation that represents addition of two values:</p>
<pre lang="fsharp">
&gt; let addTempl a b = &lt;@@ %a + %b @@&gt;;;
val addTempl : Expr&lt;int&gt; -&gt; Expr&lt;int&gt; -&gt; Expr&lt;int&gt;

&gt; eval(addTempl &lt;@@ 2 @@&gt; &lt;@@ 40 @@&gt;);;
val it : int = 42</pre>
<p>In this example, we first create a quotation template <code>addTempl</code>. This template takes two expressions as parameters and constructs an expression that represnts addition. The two expression given as an argument ar 'spliced' into the created expression using the <code>%</code> operator. Note that the holes are typed, meaning that the values that can be filled
  in the template have to be quotations representing an expression of type <code>int</code>.  </p>

<p>The splicing operator is also useful mechanism for providing 
  input data for programs that evaluate quotations. As you can see in the 
  following example, we can use it for embedding a value that represents a database table  (the <code>|&gt;</code> is a pipelining operator, 
  which applies the argument on the left hand side to the function on the right hand side). This example is based
  on the FLINQ project, which allows writing database queries in F# and executing them as SQL queries on a database
  engine:</p>
<pre lang="fsharp">
&gt; &lt;@@ (%db).Customers 
      |&gt; filter (fun x -&gt; x.City = "London")
      |&gt; map (fun x -&gt; x.Name) @@&gt;
val it : Expr&lt;seq&lt;string&gt;&gt;</pre>
<p>In the raw representation, the spliced value can be recognized using the <code>LiftedValue</code> pattern, which returns a 
  value of type <code>obj</code>, which can contain any F# value.</p>

<h3>Quoting Top-Level Definitions</h3>    
<p>The second option for quoting F# code is by explicitly marking top-level definitions with an attribute that 
  instructs the F# compiler to capture the quotation of the entire definition body. This option is sometimes 
  called <em>non-intrusive meta-programming</em>, because it allows processing of the member body (e.g. translating it to 
  some other language and executing it heterogeneously), but doesn’t require any deep understanding of meta-programming 
  from the user of the library. The following code gives a simple example:</p>
<pre lang="fsharp">
[&lt;ReflectedDefinition&gt;]
let addOne x = 
  x + 1</pre>
<p>The quotation of a top-level definition (which can be either a function or a class member) annotated using the 
  <code>ReflectedDefinition</code> attribute is then made available through the F# quotation library at runtime using 
  the reflection mechanism described earlier, but the member is still available as a compiled code and can be executed.</p>
<p>When a quotation represents a use of a top-level definition it is possible to check if this top-level definition
  was annotated using the <code>ReflectedDefinition</code> attribute and so the quotation of the definition is accessible.
  This can be done using the <code>ResolveTopDefinition</code> function as demonstrated in the following example:</p>  
<pre lang="fsharp">
let expandedQuotation = 
  match &lt;@@ addOne @@&gt; with 
  | Lambda(_, Call(_, mi, _)) -&gt; 
      match mi with
      | MethodWithReflectedDefinition(quot) -&gt; quot
      | _ -&gt; faliwith "Quotation not available!"
  | _ -&gt; failwith "Not a method use!"      
</pre>  

<h3>Using Active Patterns with Quotations</h3>
<p>As already mentioned, the programmatic access to F# quotation trees uses F# active patterns, 
  which allow the internal representation of quotation trees to be hidden while still allowing the use of 
  pattern matching as a convenient way to decompose and analyze F# quotation terms. Active-patterns
  can be also used when implementing a quotation processor, because they can be used to group similar cases 
  together. In the following example we declare an active pattern that recognizes two binary operations:</p>
<pre lang="fsharp">
let plusOp  = &lt;@@ (+) @@&gt;
let minusOp = &lt;@@ (-) @@&gt;
let (|BinaryOp|_|) x =
  match x with
  | SpecificCall plusOp (_, [l; r]) -&gt; Some("+", l, r)
  | SpecificCall minusOp (_, [l; r]) -&gt; Some("-", l, r)
  | _ -&gt; None

let rec eval x =
  match x with
  | BinaryOp (op, l, r) -&gt;
      if (op = "+") then (eval l) + (eval r)
        else (eval l) - (eval r)
  (* ... *)
</pre>
<p>In this example we declared <code>BinaryOp</code> active pattern, which can be used for matching a quotation
  that represents either addition or subtraction. In a code that processes quotations, grouping of related
  cases together by using active patterns is very useful, because you can define active patterns for all
  quotation types that your translator or analyzer can process, factor out all the code that recognizes all the supported
  cases and keep the translator or analyzer itself very simple.</p>

<h2>Article Series Links</h2>
<ul>
  <li><a href="/blog/fsharp-i-introduction.aspx">F# Overview (I.) - Introduction</a></li>
  <li><a href="/blog/fsharp-ii-functional.aspx">F# Overview (II.) - Functional Programming</a></li>
  <li><a href="/blog/fsharp-iii-oop.aspx">F# Overview (III.) - Object Oriented and Imperative Programming </a></li>
  <li>F# Overview (IV.) - Language Oriented Programming</li>
</ul>  

<h2>References<a name="fsintro4links"></a></h2>
<ul>
  <li>[1] <a type="external" href="http://blogs.msdn.com/dsyme/archive/2007/09/22/some-details-on-f-computation-expressions-aka-monadic-or-workflow-syntax.aspx">Some Details on F# Computation Expressions</a> - Don Syme's WebLog on F# and Other Research Projects</li>
  <li>[2] <a type="external" href="http://blogs.msdn.com/dsyme/archive/2007/10/11/introducing-f-asynchronous-workflows.aspx">Introducing F# Asynchronous Workflows</a> - Don Syme's WebLog on F# and Other Research Projects</li>
</ul>
